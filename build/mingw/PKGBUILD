# Maintainer: Alexander Hunziker <alex.hunziker@gmail.com>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Salamandar <felix@piedallu.me>

_pkgname=babl
pkgname="${MINGW_PACKAGE_PREFIX}-${_pkgname}-git"
pkgver=0.1.38.29.gd6e78b1
pkgrel=1
pkgdesc="babl is a dynamic, any to any, pixel format translation library."
arch=('i686' 'x86_64')
url="https://www.gegl.org/babl"
license=('LGPL3')
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-meson"
)
provides=("${MINGW_PACKAGE_PREFIX}-babl=${pkgver}")
conflicts=("${MINGW_PACKAGE_PREFIX}-babl")
options=(!libtool)
#source=(git://git.gnome.org/babl)
source=(git+https://github.com/salamandar/babl)
md5sums=('SKIP')
_gitname=babl

build() {
    mkdir "${srcdir}/build" -p

    meson "${srcdir}/${_gitname}"\
          "${srcdir}/build" \
        --prefix=/${MINGW_PREFIX} \
        --buildtype=release \
        -Db_lto=true \
        -Dwith-docs=false

    ninja -C "${srcdir}/build"
}

package() {
    DESTDIR="${pkgdir}" ninja -C "${srcdir}/build" install
}

pkgver() {
    cd "${_gitname}"
    git describe --always | sed -e 's/BABL_//g' -e 's/[_-]/./g'
}

check() {
    meson test -C "${srcdir}/build"
}
